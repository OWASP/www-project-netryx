plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.owasp.netryx'
version = '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    api project(":memory")
    api 'org.owasp.antisamy:antisamy:1.7.5'
    api 'org.slf4j:slf4j-api:2.0.9'
}

java {
    withJavadocJar()
    withSourcesJar()
}

def isReleaseVersion = !version.endsWith("SNAPSHOT")

jar {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            artifact jar

            pom {
                groupId = 'org.owasp.netryx'
                artifactId = 'armor'
                version = '1.0.1'
                name = 'OWASP Netryx Armor'
                description = 'Advanced web security framework for Java'
                url = 'https://github.com/OWASP/www-project-netryx'

                licenses {
                    license {
                        name = 'BSD-2 Clause License'
                        url = 'https://github.com/OWASP/www-project-netryx?tab=BSD-2-Clause-1-ov-file'
                    }
                }

                scm {
                    url = 'https://github.com/OWASP/www-project-netryx'
                    connection = 'scm:git:git://github.com/OWASP/www-project-netryx.git'
                    developerConnection = 'scm:git:git://github.com/OWASP/www-project-netryx.git'
                }

                developers {
                    developer {
                        id = 'satira'
                        name = 'Rauf Aliyev'
                        email = 'r.aliyev@exploit.org'
                        organizationUrl = 'https://exploit.org'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"

            name = "OSSRH"
            url = isReleaseVersion ? releaseRepo : snapshotRepo

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}

test {
    useJUnitPlatform()
}